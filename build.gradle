plugins {
  id 'eclipse'
  id 'java'
  id 'jacoco'
  id 'com.github.kt3k.coveralls' version '2.6.3'
  id 'osgi'
  id 'signing'
  id 'com.jfrog.bintray' version '1.6'
  id 'maven-publish'
}


def getVersionName = {
  try {
    def stdout = new ByteArrayOutputStream()
    exec {
      commandLine 'git', 'describe', '--tags'
      standardOutput = stdout
    }
    // strip of leading "v"
    return stdout.toString().trim().substring(1)
  }
  catch (ignored) {
    return null
  }
}


sourceCompatibility = 1.8
targetCompatibility = 1.8
project.group = 'org.pharmgkb'
project.version = getVersionName()
archivesBaseName = "pharmcat"

ext {
  description = 'Common code shared among all PharmGKB projects.'
  url = 'https://github.com/PharmGKB/PharmCAT.git'
  scm = 'scm:git@github.com:PharmGKB/PharmCAT.git'
  orgName = 'PharmGKB'
  orgUrl = 'https://www.pharmgkb.org'
  // grab properties from environment for Travis CI
  gpgPwd = project.hasProperty('signing.password') ? project.getProperty('signing.password') : System.getenv('SIGNING_PASSWORD')
  sonatypeUser = project.hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('OSSRH_USERNAME')
  sonatypePwd  = project.hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('OSSRH_PASSWORD')
  bintrayUser = project.hasProperty('bintrayUsername') ? bintrayUsername : System.getenv('BINTRAY_USERNAME')
  bintrayKey  = project.hasProperty('bintrayApiKey')   ? bintrayApiKey   : System.getenv('BINTRAY_API_KEY')
}


repositories {
  jcenter()
}

dependencies {
  compile (
      [group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'],
      [group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'],
      [group: 'com.google.code.gson', name: 'gson', version: '2.6.2'],
      [group: 'com.google.guava', name: 'guava', version: '19.0'],
      [group: 'commons-cli', name: 'commons-cli', version: '1.3.1'],
      [group: 'commons-io', name: 'commons-io', version: '2.4'],
      [group: 'com.google.api-client', name: 'google-api-client', version: '1.21.0'],
      [group: 'com.google.apis', name: 'google-api-services-drive', version: 'v3-rev23-1.21.0'],
      [group: 'com.google.http-client', name: 'google-http-client-gson', version: '1.21.0'],
      [group: 'com.google.gdata', name: 'core', version: '1.47.1'],
      [group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'],
      [group: 'org.pharmgkb', name: 'pgkb-common', version: '0.2.0'],
      [group: 'org.pharmgkb', name: 'pgkb-common-io', version: '0.1.0'],
      [group: 'org.pharmgkb', name: 'vcf-parser', version: '0.2.0'],
      [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'],
  )

  testCompile(
      [group: 'junit', name: 'junit', version: '4.12']
  )
}


jacocoTestReport {
  reports {
    xml.enabled = true // coveralls plugin depends on xml format report
    html.enabled = true
  }
}


jar {
  manifest {
    attributes 'Implementation-Title': project.name
    attributes 'Implementation-Version': project.version
    instruction 'Import-Package',
        'javax.annotation;resolution:=optional',
        '*'
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}


signing {
  sign configurations.archives
}


bintray {
  user = project.ext.bintrayUser
  key = project.ext.bintrayKey
  publications = [ 'PgkbPublication' ]
  dryRun = false
  publish = true

  pkg {
    repo = 'maven'
    name = project.name
    desc = project.ext.description
    userOrg = 'pharmgkb'
    licenses = [ 'MPL-2.0' ]
    issueTrackerUrl = project.ext.url + '/issues'
    vcsUrl = project.ext.url
    githubRepo = 'PharmGKB/PharmCAT'

    version {
      name = project.version
      vcsTag = 'v' + project.version
      gpg {
        sign = true
        passphrase = project.ext.gpgPwd
      }
      mavenCentralSync {
        sync = true
        user = project.ext.sonatypeUser
        password = project.ext.sonatypePwd
        close = '1'
      }
    }
  }
}

publishing {
  publications {
    PgkbPublication(MavenPublication) {
      from components.java
      artifact javadocJar
      artifact sourcesJar

      pom.withXml {
        asNode().children().last() + {
          resolveStrategy = Closure.DELEGATE_FIRST
          name project.name
          packaging 'jar'
          description project.ext.description
          url project.ext.url
          scm {
            url project.ext.url
            connection project.ext.scm
            developerConnection project.ext.scm
          }
          licenses {
            license {
              name 'MPL 2.0'
              url 'http://mozilla.org/MPL/2.0/'
              distribution 'repo'
            }
          }
          organization {
            name project.ext.orgName
            url project.ext.orgUrl
          }
          developers {
            developer {
              id 'markwoon'
              name 'Mark Woon'
            }
            developer {
              id 'whaleyr'
              name 'Ryan Whaley'
            }
            developer {
              id 'greytwist'
              name 'Greyson Twist'
            }
          }
        }
      }
    }
  }
}
