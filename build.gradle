plugins {
  id 'java'
  id 'application'
  id 'jacoco'
  id 'com.github.kt3k.coveralls' version '2.11.0'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
  id 'org.owasp.dependencycheck' version '6.1.5'
}


def getVersionName() {
  def errOut = new ByteArrayOutputStream()
  try {
    def stdOut = new ByteArrayOutputStream()
    exec {
      commandLine 'git', 'describe', '--tags'
      standardOutput = stdOut
      errorOutput = errOut
    }
    // strip of leading "v"
    return stdOut.toString().trim().substring(1)
  } catch (ignore) {
    logger.info('Error running git describe (' + errOut.toString().trim() + '), defaulting to 0.0.0')
    return '0.0.0'
  }
}


sourceCompatibility = 1.8
targetCompatibility = 1.8
project.group = 'org.pharmgkb'
project.version = getVersionName()
archivesBaseName = "pharmcat"
mainClassName = "org.pharmgkb.pharmcat.PharmCAT"

ext {
  moduleName = 'org.pharmgkb.pharmcat'
  description = 'The Pharmacogenomic Clinical Annotation Tool'
  url = 'https://github.com/PharmGKB/PharmCAT.git'
  scm = 'scm:git@github.com:PharmGKB/PharmCAT.git'
  orgName = 'PharmGKB'
  orgUrl = 'https://www.pharmgkb.org'
}


repositories {
  jcenter()
}

dependencies {
  implementation(
      [group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'],
      [group: 'commons-cli', name: 'commons-cli', version: '1.4'],
      [group: 'commons-io', name: 'commons-io', version: '2.8.0'],
      [group: 'com.google.api-client', name: 'google-api-client', version: '1.23.0'],
      [group: 'com.google.apis', name: 'google-api-services-drive', version: 'v3-rev108-1.23.0'],
      [group: 'com.google.code.gson', name: 'gson', version: '2.8.6'],
      [group: 'com.google.guava', name: 'guava', version: '30.1-jre'],
      [group: 'com.google.gdata', name: 'core', version: '1.47.1'],
      [group: 'com.google.http-client', name: 'google-http-client-gson', version: '1.22.0'],
      [group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'],
      [group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'],
      [group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'],
      [group: 'org.apache.commons', name: 'commons-text', version: '1.9'],
      [group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'],
      [group: 'org.checkerframework', name: 'checker-qual', version: '3.10.0'],
      [group: 'org.pharmgkb', name: 'pgkb-common', version: '0.2.2'],
      [group: 'org.pharmgkb', name: 'pgkb-common-io', version: '0.1.5'],
      [group: 'org.pharmgkb', name: 'vcf-parser', version: '0.2.1'],
      [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'],
      [group: 'com.github.jknack', name: 'handlebars', version: '4.2.0'],
  )

  testImplementation(
      [group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.7.1'],
      [group: 'org.hamcrest', name: 'hamcrest', version: '2.2']
  )
}


test {
  useJUnitPlatform()
  testLogging {
    // set options for log level LIFECYCLE
    events 'failed', 'standard_error'
    exceptionFormat 'full'
  }
}


jacocoTestReport {
  reports {
    xml.enabled = true // coveralls plugin depends on xml format report
    html.enabled = true
  }
}


jar {
  inputs.property('moduleName', project.ext.moduleName)
  manifest {
    attributes 'Automatic-Module-Name': project.ext.moduleName
    attributes 'Implementation-Title': project.name
    attributes 'Implementation-Version': project.version
    attributes 'Main-Class': mainClassName
  }
}

shadowJar {
  manifest {
    attributes 'Implementation-Title': project.name
    attributes 'Implementation-Version': project.version
  }
}

tasks.withType(Javadoc) {
  // disable the crazy super-strict doclint tool in Java 8
  options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier.set('javadoc')
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  archiveClassifier.set('sources')
  from sourceSets.main.allSource
}

task updateData(type: JavaExec, dependsOn: 'classes') {
  main = 'org.pharmgkb.pharmcat.util.DataManager'
  classpath = sourceSets.main.runtimeClasspath
  def argList = ['-a', file('src/main/resources/org/pharmgkb/pharmcat/definition/alleles'),
                 '-m', file('src/main/resources/org/pharmgkb/pharmcat/reporter'),
                 '-g', file('src/main/resources/org/pharmgkb/pharmcat/reporter/guidelines')]
  args argList
}

// NOTE: update .github/workflows/release.yml if changing filename
task extractPositions(type: JavaExec, dependsOn: 'classes') {
  main = 'org.pharmgkb.pharmcat.definition.ExtractPositions'
  classpath = sourceSets.main.runtimeClasspath
  def argList = ['-o', file('build/pharmcat_positions_' + getVersionName() + '.vcf')]
  args argList
}

// NOTE: update .github/workflows/release.yml if changing filename
task extractIntervals(type: JavaExec, dependsOn: 'classes') {
  main = 'org.pharmgkb.pharmcat.definition.ChromosomalIntervals'
  classpath = sourceSets.main.runtimeClasspath
  def argList = ['-o', file('build/pharmcat_intervals_' + getVersionName() + '.txt')]
  args argList
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}
